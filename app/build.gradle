buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.3'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.amma.yatharthakannadatv"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1001
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
        }
        debug {
            ext.enableCrashlytics = true
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
    }
    // If you use each() to iterate through the variant objects,
    // you need to start using all(). That's because each() iterates
    // through only the objects that already exist during configuration timeâ€”
    // but those object don't exist at configuration time with the new model.
    // However, all() adapts to the new model by picking up object as they are
    // added during execution.
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "yatharthakannadatv.${variant.name}-${variant.versionName}.apk"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    /**
     * The splits block is where you can configure different APK builds that
     * each contain only code and resources for a supported screen density or
     * ABI. You'll also need to configure your build so that each APK has a
     * different versionCode.
     */
    splits {
        // Settings to build multiple APKs based on screen density.
        density {

            // Enable or disable building multiple APKs.
            enable false

            // Exclude these densities when building multiple APKs.
            exclude "ldpi", "tvdpi", "xxxhdpi", "400dpi", "560dpi"
        }
    }
}

/*
* Don't use 23.+ - always use complete version number eg: 23.4.0
* */
ext {
    compileSdkVersion  = '27.1.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$compileSdkVersion"
    implementation "com.android.support:support-v4:$compileSdkVersion"
    implementation "com.android.support:design:$compileSdkVersion"
    implementation ("com.crashlytics.sdk.android:crashlytics:2.9.1@aar") {
        transitive = true
    }
    implementation ('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }
}
